apply from: '../versions.gradle'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
        classpath 'com.google.gms:google-services:3.1.1'
        classpath 'io.fabric.tools:gradle:1.26.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
//apply plugin: 'io.fabric' // TODO: uncomment when having set the Fabric API key
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getSignInformation = { keystorePath, keystorePropertyPath ->
    def keyStore = file(keystorePath)
    if (!keyStore.exists()) {
        logger.error("Keystore file not found, path=${keyStore.absolutePath}")
        return
    }
    def propertiesFile = file(keystorePropertyPath)
    if (!propertiesFile.exists()) {
        logger.error("Keystore properties file not found, path=${propertiesFile.absolutePath}")
        return
    }
    Properties properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return [
            storeFile    : keyStore,
            storePassword: properties["store_password"],
            keyAlias     : properties["key_alias"],
            keyPassword  : properties["key_password"]
    ]
}

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion
    defaultConfig {
        applicationId 'com.xmartlabs.template'
        minSdkVersion project.ext.minSdkVersion
        multiDexEnabled true
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.librariesVersionCode
        versionName project.ext.librariesVersion
    }
    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        debug {
            def signInformation = getSignInformation("debug.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
        //noinspection GroovyMissingReturnStatement
        release {
            def signInformation = getSignInformation("release.keystore", "keystore.properties")
            if (signInformation != null) {
                storeFile signInformation.storeFile
                storePassword signInformation.storePassword
                keyAlias signInformation.keyAlias
                keyPassword signInformation.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            //noinspection GrUnresolvedAccess
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //noinspection GrUnresolvedAccess,GroovyAssignabilityCheck
            //signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'version', 'env'
    productFlavors {
        //noinspection GroovyMissingReturnStatement
        production {
            dimension 'env'
        }

        staging {
            dimension 'env'
            applicationIdSuffix '.staging'
        }

        lollipopAndAbove {
            dimension 'version'
            minSdkVersion 21 // For faster multidex compilation
        }

        allVersions {
            dimension 'version'
        }
    }
    //noinspection GroovyMissingReturnStatement
    lintOptions {
        checkAllWarnings true
        ignore 'GradleDependency', 'NewerVersionAvailable' // To avoid flaky builds when the dependencies are updated
        warningsAsErrors false
        abortOnError false
    }
    packagingOptions {
        exclude '.readme'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    api project(path: ':core')
    api project(path: ':log-crashlytics')
    api project(path: ':mvp')
    api project(path: ':retrofit')
    api project(path: ':ui')

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:${libraryVersion.androidSupport}"
    implementation "androidx.cardview:cardview:${libraryVersion.androidSupport}"
    implementation "com.google.android.material:material:${libraryVersion.androidSupport}"
    implementation "androidx.recyclerview:recyclerview:${libraryVersion.androidSupport}"
    implementation "androidx.legacy:legacy-support-v13:${libraryVersion.androidSupport}"
    implementation "com.f2prateek.dart:dart:${libraryVersion.dart}"
    implementation "com.f2prateek.dart:henson:${libraryVersion.dart}"
    implementation "com.github.mrmike:Ok2Curl:${libraryVersion.ok2Curl}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${libraryVersion.dbFlow}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${libraryVersion.dbFlow}"
    implementation "com.github.xmartlabs:Traceur:${libraryVersion.traceur}"
    implementation "com.google.dagger:dagger:${libraryVersion.dagger}"
    implementation "com.google.dagger:dagger-android-support:${libraryVersion.dagger}"
    implementation "com.hannesdorfmann.fragmentargs:annotation:${libraryVersion.fragmentArgs}"
    implementation "com.jakewharton.threetenabp:threetenabp:${libraryVersion.localDateBackport}"
    implementation "com.jakewharton.timber:timber:${libraryVersion.timber}"
    implementation "com.squareup.okhttp3:logging-interceptor:${libraryVersion.okHttp}"
    implementation "com.squareup.okhttp3:okhttp:${libraryVersion.okHttp}"
    implementation "com.squareup.okio:okio:${libraryVersion.okio}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${libraryVersion.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${libraryVersion.retrofit}"
    implementation "com.squareup.retrofit2:retrofit:${libraryVersion.retrofit}"
    implementation "com.trello.rxlifecycle3:rxlifecycle-components:${libraryVersion.rxLifecycle}"
    implementation "com.trello.rxlifecycle3:rxlifecycle:${libraryVersion.rxLifecycle}"
    implementation "com.wdullaer:materialdatetimepicker:${libraryVersion.materialDateTimePicker}"
    implementation "io.reactivex.rxjava2:rxandroid:${libraryVersion.rxAndroid}"
    implementation "io.reactivex.rxjava2:rxjava:${libraryVersion.rxJava}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${libraryVersion.kotlin}"
    implementation "com.github.square.picasso:picasso:${libraryVersion.picasso}"
    implementation 'com.viewpagerindicator:library:2.4.1'

    kapt "com.f2prateek.dart:dart-processor:${libraryVersion.dart}"
    kapt "com.f2prateek.dart:henson-processor:${libraryVersion.dart}"
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${libraryVersion.dbFlow}"
    kapt "com.google.dagger:dagger-android-processor:${libraryVersion.dagger}"
    kapt "com.google.dagger:dagger-compiler:${libraryVersion.dagger}"
    kapt "com.hannesdorfmann.fragmentargs:processor:${libraryVersion.fragmentArgs}"

    compileOnly 'javax.annotation:jsr250-api:1.0'
}

//apply plugin: 'com.google.gms.google-services' // TODO: enable when adding google-services.json
